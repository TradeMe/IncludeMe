apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

project.group = project.ext.group
project.version = project.ext.version

bintray {
    user = project.hasProperty("BINTRAY_USERNAME") ? project.property('BINTRAY_USERNAME') : null
    key = project.hasProperty("BINTRAY_API_KEY") ? project.property('BINTRAY_API_KEY') : null
    pkg {
        repo = project.ext.name
        name = getArtifactId()
        userOrg = 'trademe'
        licenses = ['Apache-2.0']
        vcsUrl = project.ext.scm
        version {
            name = project.version
            desc = project.ext.description
            released = new Date()
            vcsTag = project.version
        }
        publications = ['Bintray']
    }
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    scm {
        url ext.scm
    }
}

publishing.publications {
    Bintray(MavenPublication) {
        from components.java

        groupId project.ext.group
        artifactId getArtifactId()
        version project.ext.version

        pom.withXml {
            def root = asNode()
            root.appendNode('description', project.ext.description)
            root.appendNode('name', project.ext.name)
            root.appendNode('url', project.ext.scm)
            root.children().last() + pomConfig
        }
    }
}

def getArtifactId() {
    return getArtifactId(project)

}

def getArtifactId(someProject) {
    if (!someProject.ext.has('artifactId')) {
        //default artifactId is the project/module name
        return someProject.getName()

    }
    return someProject.ext.artifactId
}